;THIS INCLUDE FILE DOES THE PULSES NEEDED TO FOIL THE NES LOCK CHIP.
;IT DOES A TRAIN OF PULSES THAT MATCH THE CHARACTERISTICS OF THE KEY CHIP
;TO SOME EXTENT.  THIS IS INTENDED TO FOOL NINTENDO INTO THINKING OUR 
;CIRCUIT TAKES ADVANTAGE OF A BUG IN THEIR LOCK CHIP SOFTWARE WHEN IN
;FACT THE -.7 VOLTS GENERATED IN THE LOW OF THE PULSES IS THE REAL
;KEY CHIP FOILER.

	JSR	DX12  
	LDX	#83
	JSR	DX15  		;START WITH A MISSING PULSE TO GET A GOOD LOW

	LDY	#4
DX10:	JSR	DX12  		;THEN PUT IN 4 HIGH PULSES TO LOOK LINE A #
	DEY
	BNE	DX10
	LDX	#83
	JSR	DX15  		;FOLLOW WITH A MISSING PULSE

	JSR	DX12  

	LDA	#12
	STA	TWELVE		;RAISE BOTH LINES WHEN DONE
	JMP	DX20

;THIS SUB MAKES A 3 uS HIGH PULSE FOLLOWED BY A 77 uS PAUSE.  Y IS NOT CHANGED.  
;ACTUALLY THIS CODE DELAYS NEARLY 3 TIMES TOO LONG.  REMOVING THE PUSHES
;AND POPS AND NOPS WOULD BE BETTER BUT IT DIDN'T WORK (THE GAME BOOTED WITH 
;GARBAGE SOMETIMES).  WE WENT WITH THE LONGER PULSES FOR NOW.

DX12:	LDA	#8
	STA	EIGHT		;PUT OUT A HIGH
	LDA	#0
	PHA			;STALL TO MAKE IT ABOUT 3us
	PLA			
	NOP
	NOP
	STA	ZERO		;THEN GO LOW

;DELAY 77 uS.  THIS CODE MUST NOT PASS A PAGE BOUNDRY.  Y IS NOT CHANGED.
;ACTUALLY THIS CODE DELAYS NEARLY 3 TIMES TOO LONG.  A VALUE OF ABOUT 28
;WOULD BE BETTER FOR X BUT IT DIDN'T WORK (THE GAME BOOTED WITH GARBAGE
;SOMETIMES).  WE WENT WITH THE LONGER PULSES FOR NOW.

	LDX	#80
DX15:	DEX			;ENTER HERE WITH X=83 FOR 80 uS PAUSE
	BNE	DX15
	RTS

ZERO:	.byte	0
EIGHT:	.byte	8
TWELVE:	.byte	12		;This should be at $0057 in the rom

DX20:
